apply plugin: 'com.android.library'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.2'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:support-v4:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
}

/*** ARTIFACTORY ***/
apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

String publishRepoKey = version.endsWith('SNAPSHOT') ? 'libs-snapshot-local' : 'libs-release-local'

publishing {
    publications {
        aar(MavenPublication) {
            groupId = 'com.beanstream.android'
            artifactId project.name
            version = version
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"

            pom.withXml {
                def root = asNode()
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

artifactory {
    contextUrl = "${bic_artifactory_url}"
    publish {
        repository {
            repoKey = publishRepoKey
            username = "${bic_artifactory_user}"
            password = "${bic_artifactory_password}"
            maven = true
        }
        defaults {
            publishArtifacts = true
            publications('aar')
        }
    }
}
